// BU1_16 - officer, smoking and talking to two soldiers
// -------------------------------------------------------------------------------------------------------------
// Variables definition
Block
{
Frame barelx;
	FRM_FindFrame(barelx, "BU1_bar03");
Frame xplo;
	FRM_FindFrame(xplo, "BU1_explosion");
Frame xplosives;
	FRM_FindFrame(xplosives, "BU1_explosives");
Frame bu1_17;
	FRM_FindFrame(bu1_17, "BU1_17");
Frame bu1_18;
	FRM_FindFrame(bu1_18, "BU1_18");
Frame bu1_19;
	FRM_FindFrame(bu1_19, "BU1_19");
Frame bu1_20;
	FRM_FindFrame(bu1_20, "BU1_20");
Frame bu1_37;
	FRM_FindFrame(bu1_37, "BU1_37");
Frame me;
	FRM_GetMyFrame(me);
}
// -------------------------------------------------------------------------------------------------------------
// Alarm and AI event settings
// vsetky typy alarmov nastavene na true
//     1 - blizka strelba
//     2 - strelba
//     4 - kroky
//     8 - zvuky (OnUse)
//   16 - explozia
//   32 - vidim
//   64 - zranenie
// 128 - smrtelny vykrik
// 256 - friendly fire
// 512 - vidim mrtvolu
SetAlarmType(12, false);
// -------------------------------------------------------------------------------------------------------------
// Whenever keywords and signals
OnAlarm()
{
	HUMAN_SetAnim("", 200, 200, false);
	HUMAN_ACTIVITY_Smoke(false);
	HUMAN_SETMODE_Run();
	HUMAN_SETSIGHTRANGE(55);
	EndScript();
}

OnAlarmDone()
{
	HUMAN_SetAlarm(false);
	GoTo end;
}

Whenever player(_PlayerInRange(60))
{
	HUMAN_Suspend(false);
	GoTo start;
}

Whenever explo(_SignalReceived(1))	// received from BU1_timer
{
	GoTo destroyevidence;
}

OnDeath()
{
EndScript();
}
// -------------------------------------------------------------------------------------------------------------
// Starting pose
HUMAN_SETAIMODE_Defensive();
FRM_SwitchFaceTexture(me, "e_f046");
HUMAN_Suspend(true);
FRM_SetOn(xplosives, false);
GoTo end;

Label start:
	HUMAN_ACTIVITY_Smoke(true);
	Delay(60000);
	HUMAN_SetAnim("%%kourimN", 500, 500, true);
	Delay(20000);
	HUMAN_SetAnim("", 500, 500, false);
	Delay(60000);
	HUMAN_ACTIVITY_Smoke(false);
	Delay(10000);
	HUMAN_SetAnim("%%lenin", 500, 500, true);
	Delay(60000);
	HUMAN_SetAnim("", 500, 500, false);
	Delay(10000);
	GoTo start;

Label destroyevidence:
	HUMAN_Move("BU1_16_03");
	HUMAN_Move("BU1_16_04");
	HUMAN_SetAnim("%%mina1a", 200, 200, false);
	Delay(1300);
	FRM_SetOn(xplosives, true);
	Delay(800);
	SendSignal(bu1_17, 4);
	SendSignal(bu1_18, 4);
	SendSignal(bu1_19, 1);
	SendSignal(bu1_20, 1);
	SendSignal(bu1_37, 1);
	Delay(1000);
	HUMAN_SETMODE_Run();
	HUMAN_Move("BU1_16_05");
	HUMAN_SETMODE_Crouch();
	Delay(10000);
	SendSignal(barelx, 2);
	SendSignal(xplo, 1);

Label end: